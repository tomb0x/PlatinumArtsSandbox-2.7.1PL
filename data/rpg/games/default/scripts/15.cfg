//an apple tree script! shock horror!

r_script_signal spawn [
	r_additem self 1 (rnd 4)
]

r_script_signal interact [
	r_chat self (? (r_get_amount self 1) 1 0)
]

r_script_say "[You examine the canopy, alas you do not spy any ripe fruits among the branches]" [ //0
	r_response "[Leave it alone]" -1
]

r_script_say "[You examine the canopy and sight ripened apples among its branches]" [ //1
	r_response "[Strength: Kick the trunk]" (? (>= (r_get_attr player $STAT_STRENGTH) 40) 3 2)
	r_response "[Agility: Climb the tree]" (? (>= (r_get_attr player $STAT_AGILITY) 50) 8 7)
	r_response "[Chop it down]" (? (r_get_amount player 4) 10 9)
	r_response "[Leave it alone for now]" -1
]

r_script_say "[Failure: Your kicks are greeted with nought but leaves]" [ // 2
	r_response "[Continue]" 1
]

r_script_say "[Success: You send a shockwave up the tree and a few apples begin to fall]" [ //3
	r_response "[Agility: Catch them gently]" (? (>= (r_get_attr player $STAT_AGILITY) 40) 5 4)
	if (r_get_amount player 5) [
		r_response "[Use the large piece of cloth to catch the apples]" 6
	]
]

r_script_say "[Failure: The apples narrowly evade your grasp and suffer significant damage from assorted stones on the ground]" [ //4
	r_response "[Depart with your booty]" -1 [
		dmg  = (+ (rnd (r_get_amount talker 1)) 1)
		r_additem player 3 $dmg
		r_additem player 1 (- (r_get_amount talker 1) $dmg)
		r_remove talker 1 (r_get_amount talker 1)
	]
]

r_script_say  "[Success: You even dive, barely saving an apple from hitting the ground. You pocket the apples you've caught and tend to your scraped elbows]" [ //5
	r_response "[Depart with your booty]" -1 [
		r_additem player 1 (r_get_amount talker 1)
		r_remove talker 1 (r_get_amount talker 1)
	]
]

r_script_say "[The apples land gently on the outstreched cloth and get a free polish to boot! You pocket the cloth and apples]" [ //6
	r_response "[Depart with your booty]" -1 [
		r_additem player 1 (r_get_amount talker 1)
		r_remove talker 1 (r_get_amount talker 1)
	]
]

r_script_say "[Failure: As you are about to grab a branch in the canopy, your feet slip and you fall to the ground below]" [ //7
	r_response "[Continue]" 1
]

r_script_say "[Success: With nimble hands you manage to climb upto te canopy. You pocket the apples and descend the tree without further incidence]" [ //8
	r_response "[Depart with your booty]" -1 [
		r_additem player 1 (r_get_amount talker 1)
		r_remove talker 1 (r_get_amount talker 1)
	]
]

r_script_say "[???: With no tools available, you punch the tree several times. You conclude your hand is likely to give way before the tree]" [ //9
	r_response "[Continue]" 1
]

r_script_say "[???: With a final swing of your axe, the tree finally gives and collapses with a satisfying thud. Your maneuver has likely caused significant damage to the fruit]" [ //10
	r_response "[Depart with your booty]" -1 [
		dmg  = (+ (rnd (r_get_amount talker 1)) 1)
		r_additem player 3 $dmg
		r_additem player 1 (- (r_get_amount talker 1) $dmg)
		r_destroy talker
	]
]
