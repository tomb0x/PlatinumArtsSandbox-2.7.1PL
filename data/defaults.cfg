// te domyślne ustawienia są wykonywane, gdy nie jest dostępny plik "config.cfg"
// nie modyfikuj niczego poniżej, zamiast tego zmień ustawienia w grze lub dodaj do autoexec.cfg

//tutaj ustaw motyw informacji ekranowych. tak naprawdę chodzi tylko o kolory...
exec data/hud/2.3.cfg

mapcomplete = [ complete $arg1 (concatword "packages/base/" (getgame)) ogz ]
mapcomplete map

//////////////////////////////////
// uniwersalne polecenia kółko przewijania + modyfikator:

defaultmodifier = 0
modifier = $defaultmodifier
domodifier = [ modifier = $arg1; onrelease [ modifier = $defaultmodifier ] ]

universaldelta = [
  do [delta_@(if $editing [ result edit ] [ result game ])_@modifier @arg1]
]

bind G [ domodifier 1 ]

// polecenia poniżej są używane wraz z kółkiem myszy (i universaldelta).
// jeśli nie posiadasz kółka przewijania myszy lub nie chcesz go używać,
// możesz albo zmienić powiązania universaldelta
// ex:  bind MOUSE4 [ universaldelta 1 ]
//      bind MOUSE5 [ universaldelta -1 ]
// można zmienić na:
//      bind INSERT [ universaldelta 1 ]
//      bind DELETE [ universaldelta -1 ]
// tak, że klawisze INSERT i DELETE działają jak kółko przewijania
//
// lub możesz ponownie powiązać wszystkie polecenia do oddzielnych klawiszy
// np:  bind T [ domodifier 6 ]
// można zmienić na:
//      bind HOME [ edittex 1 ]
//      bind END [ edittex -1 ]

delta_edit_0 = [ if $blendpaintmode [ nextblendbrush $arg1; echo (getblendbrushname (curblendbrush)) ] [ editfacewentpush $arg1 1 ] ] // Wypełnij pustą kostkę lub zmień pędzle w trybie blendpaint
delta_edit_1 = [ nodebug [ gridpower (+ $arg1 $gridpower) ] ]
delta_edit_2 = [ editfacewentpush $arg1 0 ]                     // wypchnij wybraną powierzchnię/narożnik
delta_edit_3 = [ editfacewentpush $arg1 2 ]                     // wypchnij narożnik wskazany kursorem
delta_edit_4 = [ editrotate $arg1 ]                             // obrócić o 90 stopni
delta_edit_5 = [ entproperty 0 $arg1 ]                          // i inne
delta_edit_6 = [ edittex $arg1 ]                                // zmieniaj tekstury
delta_edit_9 = [ selectbrush $arg1 ]                            // zmieniaj pędzle mapy wysokości
delta_edit_10 = [ entautoview $arg1 ]
delta_edit_11 = [ entproperty 0 (* $arg1 $multiplier) ]
delta_edit_12 = [ entproperty 1 (* $arg1 $multiplier) ]
delta_edit_13 = [ entproperty 2 (* $arg1 $multiplier) ]
delta_edit_14 = [ entproperty 3 (* $arg1 $multiplier) ]
delta_edit_15 = [ entproperty 4 (* $arg1 $multiplier) ]
delta_edit_16 = [ entproperty 5 (* $arg1 $multiplier) ]
delta_edit_17 = [ entproperty 6 (* $arg1 $multiplier) ]
delta_edit_18 = [ entproperty 7 (* $arg1 $multiplier) ]

delta_edit_19 = [ vdelta [voffset (* $arg1 $multiplier2) 0] ]   // vSlot: przesunięcie poziome H
delta_edit_20 = [ vdelta [voffset 0 (* $arg1 $multiplier2)] ]   // vSlot: przesunięcie pionowe V
delta_edit_21 = [ vdelta [vrotate $arg1] ]                      // vSlot: obrót
delta_edit_22 = [ vdelta [vscale (if (< $arg1 0) 0.5 2)] ]      // vSlot: skala

//celowo nie używany dla mutatorów, znanych z robienia kilku zabawnych rzeczy w różnych systemach operacyjnych
editbind LALT [multiplier = 10; onrelease [multiplier = 1]]
editbind RALT [multiplier2 = 32; onrelease [multiplier2 = 1]]
multiplier = 1
multiplier2 = 1


outlinecolours = [
"0x000000" "0xFFFFFF"
"0xFF0000" "0x00FF00" "0x0000FF"
"0xFFFF00" "0xFF00FF" "0x00FFFF"
"0xFF7F00" "0xFF007F" "0x7FFF00" "0x00FF7F" "0x7F00FF" "0x007FFF"
"0xFF7F7F" "0x7FFF7F" "0x7F7FFF"
]

changeoutline = [
    index = (mod (+ $arg1 (listfind c $outlinecolours [= $c $outlinecolour])) (listlen $outlinecolours))
    while [> 0 $index] [
        index = (+ (listlen $outlinecolours) $index )           //tylko upewniam się, że będzie pasować
    ]


    outlinecolour (at $outlinecolours $index)
    setcollevels col $outlinecolour
    echo (format "Outline Colour: ^f3%1 ^f0%2 ^f1%3" $col1 $col2 $col3)
]

togglemessage = [
    if (> $numargs 1) [
        echo $arg2 (? $arg1 "[wł]" "[wył]")   // `wł. ` -> musi być ze spacją bo inaczej się psuje z jakiegoś powodu. Może być bez spacji np. w nawiasach `[wł]`
    ]
]

//binds without mutators
editbind SPACE  [ cancelsel; hmapedit 0; passthroughsel 0 ]     // meh.. trzeba będzie dokonać lepszej poprawki
editbind MOUSE1 [ if $blendpaintmode paintblendmap editdrag ]
editbind MOUSE3 [ selcorners ]
editbind MOUSE2 [ if $blendpaintmode rotateblendbrush editextend ]
bind SPACE "jump"
bind MOUSE2 "jump"
specbind MOUSE1 "nextfollow"
specbind MOUSE2 "nextfollow -1"
specbind MOUSE3 "follow"
specbind SPACE "follow"
//mutators
shiftkey = 0
bind RSHIFT [shiftkey = 1; onrelease [shiftkey = 0]]
bind LSHIFT (getbind RSHIFT) //future proofing :D
ctrlkey = 0
bind RCTRL [ctrlkey = 1; onrelease [ctrlkey = 0]]
bind LCTRL (getbind RCTRL)

//powiązania czynią tworzenie łatwiejszym
nokeybind = [if (&& (= $shiftkey 0) (= $ctrlkey 0))]
shiftkeybind = [if (&& (= $shiftkey 1) (= $ctrlkey 0))]
ctrlkeybind = [if (&& (= $shiftkey 0) (= $ctrlkey 1))]
bothkeybind = [if (&& (= $shiftkey 1) (= $ctrlkey 1))]
//układ alfabetyczny
editbind A [left]                                               //zarezerwowane
editbind B [
    @nokeybind [domodifier 9; brushcopy = 0]
]
editbind C [
    @nokeybind [editcopy]
    @ctrlkeybind [editcopy]
    @shiftkeybind [clip]
]
editbind D [right]                                              //zarezerwowane
//E zarezerwowane do przełączania edycji
editbind F [
    @nokeybind [domodifier 2]
]
editbind G [
    @nokeybind [domodifier 1]
    @shiftkeybind [glass]
]
editbind H [
    @nokeybind [hmapedit (if ($hmapedit) 0 1); blendpaintmode 0]
]
//editbind I [redo]                                             //think, is I really screaming redo...
editbind J [
    @nokeybind [domodifier 9; brushcopy = 0]
]
editbind K [
    @nokeybind [calclight 1]
    @shiftkeybind [calclight 0]
    @ctrlkeybind [calclight 0]
    @bothkeybind [calclight -1]
]                                                               //hmm, może spróbuję czegoś oryginalnego
editbind L [
    @nokeybind [fullbright (= $fullbright 0); togglemessage $fullbright "Pełna jasność geometrii"]
    @shiftkeybind [lava]
]
editbind M [
    @nokeybind [showmat (= $showmat 0); togglemessage $showmat "Pokazywanie objętości materiałów"]
]
editbind N [
    @nokeybind [entselect insel]
    @ctrlkeybind [showgui newmap]
    @shiftkeybind [noclip]
]
//editbind O [
//]
editbind P [
    @nokeybind [
        hmapedit 0;
        blendpaintmode (if (= $blendpaintmode 5) 0 (+ $blendpaintmode 1));
        echo (at $paintmodes $blendpaintmode)
    ]
    @shiftkeybind [ blendpaintmode 0; echo "blendpaintoff" ]
]
editbind Q [
    @nokeybind [domodifier 3]
]
editbind R [
    @nokeybind [domodifier 4]
    @shiftkeybind [editmat respawn]
]
editbind S [backward]                                           //zarezerwowany
editbind T [
    @nokeybind [saycommand]
]
editbind U [
    @nokeybind [undo; passthroughsel 0]
]
editbind V [
    @nokeybind [editpaste]
    @ctrlkeybind [editpaste]
]
editbind W [forward]                                            //zarezerwowany
editbind X [
    @nokeybind [editcut]
    @ctrlkeybind [editflip]
]

editbind Y [
    @nokeybind [domodifier 6]
    @ctrlkeybind [redo]
    @shiftkeybind [showtexgui]
]
editbind Z [
    @nokeybind [undo; passthroughsel 0]
    @ctrlkeybind [undo; passthroughsel 0]
    @bothkeybind [redo]
]
//alternatywne klawisze ruchu… chociaż mogę chcieć zrobić je osobno, aby uniknąć materiałów i innych rzeczy
editbind LEFT (geteditbind A)
editbind RIGHT (geteditbind D)
editbind DOWN (geteditbind S)
editbind UP (geteditbind W)
editbind 1 [
    @nokeybind [domodifier 11]
    @shiftkeybind [showdynlights (= $showdynlights 0); togglemessage $showdynlights "podgląd dynamicznego oświetlenia w trybie edycji"]
]
editbind 2 [
    @nokeybind [domodifier 12]
]
editbind 3 [
    @nokeybind [domodifier 13]
    @shiftkeybind [showlighting (! $showlighting); togglemessage $showlighting "podgląd świateł"]
]
editbind 4 [
    @nokeybind [domodifier 14]
    @shiftkeybind [hidehud (! $hidehud); togglemessage $hidehud "ukrycie Wyświetlacza Informacji"]
]
editbind 5 [
    @nokeybind [domodifier 15]
    @shiftkeybind [editingparticles (! $editingparticles); togglemessage $editingparticles "pokazywanie cząsteczek w edycji "]
]
editbind 6 [
    @nokeybind [domodifier 16]
    @shiftkeybind [entselsnap (! $entselsnap); togglemessage $entselsnap "klejenie obiektów"]
]
editbind 7 [
    @nokeybind [domodifier 17]
    @shiftkeybind [outline (! $outline); togglemessage $outline "kontury siatek"]
    @ctrlkeybind [dtoutline (! $dtoutline); togglemessage $dtoutline "kontur testowania głębokości"]
    @bothkeybind [changeoutline 1]
]
editbind 8 [
    @nokeybind [domodifier 18]
    @shiftkeybind [wireframe (! $wireframe); togglemessage $wireframe "szkielet geometrii"]
]
//9 zarezerwowane dla widoku z trzeciej osoby
editbind 0 [
    @shiftkeybind [allfaces (! $allfaces); togglemessage $allfaces "tekstury wszystkich ścian"]
]
editbind F1 [
    @nokeybind [showgui mapmodels]
    @shiftkeybind [paused (! $paused); togglemessage $paused "zatrzymanie"]
]
editbind F2 [
    @nokeybind [showtexgui]
]
editbind F3 [
    @nokeybind [if (cleargui 1) [] [showentgui]]
]
editbind F4 [
    @nokeybind [if (cleargui 1) [] [showgui skies]]
]
editbind F5 [
    @nokeybind [if (cleargui 1) [] [showgui entfind]]
]
editbind F6 [
    @nokeybind [notepad (mapcfgname)]
]
editbind F7 [
    @nokeybind [if (cleargui 1) [] [showgui editopts]]
]

editbind COMMA  [
    @nokeybind [domodifier 10; onrelease entautoview]
]
editbind PERIOD [
    @nokeybind [selentedit]
]
editbind KP_ENTER [
    @nokeybind [entselect insel]
]

editbind BACKSPACE  [
    @nokeybind [editdel]
    @ctrlkeybind [entselect insel; editdel]
]
editbind DELETE [
    @nokeybind [editdel]
    @ctrlkeybind [entselect insel; editdel]
]
//editbind PLUS [                     // klawiatura polska (214) -> 1: 1234567890+' 2: QWERTZUIOPżś 3: ASDFGHJKLŁąó 4: YXCVBNM,.-
editbind EQUALS [
    @nokeybind    [floatspeed (? (> $floatspeed 950) 1000 (+ $floatspeed 50))]
    @ctrlkeybind  [floatspeed (? (> $floatspeed 900) 1000 (+ $floatspeed 100))]
    @shiftkeybind [floatspeed (? (> $floatspeed 990) 1000 (+ $floatspeed 10))]
    @bothkeybind  [floatspeed 1000]
    echo (format "prędkość latania = %1" $floatspeed)
]
editbind MINUS [
    @nokeybind    [floatspeed (? (< $floatspeed 50)  10 (- $floatspeed 50))]
    @ctrlkeybind  [floatspeed (? (< $floatspeed 110) 10 (- $floatspeed 100))]
    @shiftkeybind [floatspeed (? (< $floatspeed 20)  10 (- $floatspeed 10))]
    @bothkeybind  [floatspeed 10]
    echo (format "prędkość latania = %1" $floatspeed)
]

editbind KP_MINUS [rise]         // prosto w górę
editbind KP_PLUS [descent]       // prosto w dół

entcomplete = [ listcomplete $arg1 $enttypelist ]
entcomplete newent
entcomplete entfind

listcomplete editmat "air water clip glass noclip lava respawn alpha"
looplist f "air water clip glass noclip lava respawn alpha" [
  alias $f [editmat @f]
]

//Dla ludzi takich jak calimer :P

exit = [quit]


//mostly universal gamemode stuff
bind MOUSE4 [ universaldelta 1 ]
bind MOUSE5 [ universaldelta -1 ]

delta_game_0 = [ if (= $zoom $arg1) [] [zoom $arg1 ]]
delta_game_1 = [if (= $arg1 1) [if (= $fov 150) [] [fov (+ $fov 5)]] [if (= $fov 10) [] [fov (- $fov 5)]]]

bind W forward
bind A left
bind S backward
bind D right
bind T [
    @nokeybind [saycommand]
]
bind BACKQUOTE [
    @nokeybind [saycommand "/"]
]
bind SLASH [
    @nokeybind [saycommand "/"]
]
bind E [
    @nokeybind    [edittoggle]                  //mostly universal :P
    @ctrlkeybind  [if $editing showentgui]
    @shiftkeybind [showtexgui]                  //won't show unless $editing
    @bothkeybind  [notepad (mapcfgname)]
]
bind KP_MINUS [
    @nokeybind    [conskip 1]
    @shiftkeybind [conskip 5]
    @ctrlkeybind  [conskip 20]
    @bothkeybind  [conskip 1000]
]
bind KP_PLUS [
    @nokeybind    [conskip -1]
    @shiftkeybind [conskip -5]
    @ctrlkeybind  [conskip -20]
    @bothkeybind  [conskip -1000]
]
bind F1 [
    @shiftkeybind [paused (= $paused 0); togglemessage $paused "zatrzymanie"]
]
bind F11 [
    @nokeybind [toggleconsole]
]
bind F12 [
    @nokeybind [screenshot]
    @ctrlkeybind [hidehud 1; sleep 0 [screenshot; hidehud 0]]
]

bind 9 [
    @nokeybind    [thirdperson (= $thirdperson 0); togglemessage $thirdperson "widok z trzeciej osoby"]
    @ctrlkeybind  [thirdperson 0; echo "widok z pierwszej osoby"]
    @shiftkeybind [thirdperson 1; echo "widok z trzeciej osoby"]
    @bothkeybind  [thirdperson 2; echo "swobodne obroty"]               //dziwne, to nie jest liczba dwójkowa :p
]

showquickgui = [
    if ($hmapedit) [
        showgui heightfield
    ] [
        if (enthavesel) [
            showgui (et)
            initentgui
        ] [
            showgui quickedit
        ]
    ]
]

playmode = 0

sleep 0 [
    bind UP    (getbind W)
    bind LEFT  (getbind A)
    bind DOWN  (getbind S)
    bind RIGHT (getbind D)
]
