//ui templates to simplify UI creation and ease maintainability
//these should be used instead of the raw commands to keep things consistent and easily changeable.
//All fo these are prefixed with UI

//Quick n' Easy means of displaying state - just use as a child
UIIdle =     [ ]
UIHover =    [ uimodcolor .80 .80 .80 0 0 [uiclamp 1 1 1 1]]
UISelected = [ uimodcolor .60 .60 .60 0 0 [uiclamp 1 1 1 1]]
UIDisabled = [ uimodcolor .20 .20 .20 0 0 [uiclamp 1 1 1 1]]

//Creates a basic menu with a title and a close button
//args: name, contents, title
UIMenu = [
	(concatword "show" $arg1) = [
		showui [@@arg1] [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left" 0.01 0.04
				uistretchedimage "data/newui/edge_top" 0 0.04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
					UICloseButton [@@@@@arg1]
				]
				uistretchedimage "data/newui/corner_top_right" 0.01 0.04

				uistretchedimage "data/newui/edge_left" 0.01 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [
					uiclamp 1 1 1 1
					@@@@arg2
				]
				uistretchedimage "data/newui/edge_right" 0.01 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" 0.01 0.01
				uistretchedimage "data/newui/edge_bottom" 0 0.01 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/corner_bottom_right" 0.01 0.01
			]
		]
	]
]

//like the above, but without a close button
//args: name, contents, title
UIMenuNoClose = [
	(concatword "show" $arg1) = [
		showui [@@arg1] [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left" 0.01 0.04
				uistretchedimage "data/newui/edge_top" 0 0.04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
				]
				uistretchedimage "data/newui/corner_top_right" 0.01 0.04

				uistretchedimage "data/newui/edge_left" 0.01 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [
					uiclamp 1 1 1 1
					@@@@arg2
				]
				uistretchedimage "data/newui/edge_right" 0.01 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" 0.01 0.01
				uistretchedimage "data/newui/edge_bottom" 0 0.01 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/corner_bottom_right" 0.01 0.01
			]
		]
	]
]

//like the above, but without a title
//args: name, contents
UIMenuNoTitle = [
	(concatword "show" $arg1) = [
		showui [@@arg1] [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left_thin" 0.01 0.01
				uistretchedimage "data/newui/edge_top_thin" 0 0.01
				uistretchedimage "data/newui/corner_top_right_thin" 0.01 0.01

				uistretchedimage "data/newui/edge_left" 0.01 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [
					uiclamp 1 1 1 1
					@@@@arg2
				]
				uistretchedimage "data/newui/edge_right" 0.01 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" 0.01 0.01
				uistretchedimage "data/newui/edge_bottom" 0 0.01 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/corner_bottom_right" 0.01 0.01
			]
		]
	]
]

//Creates a simple close button
//args: menu
UICloseButton = [
	uibutton [ hideui [@@arg1] ] [
		uialign 1 0
		uiimage "data/newui/menu_close" 0.03 0.03 UIIdle
		uiimage "data/newui/menu_close" 0.03 0.03 UIHover
		uiimage "data/newui/menu_close" 0.03 0.03 UISelected
	]
]

//creates a button with a centred text element
//args: text, action, children
UIButton = [
	uibutton [ @arg2 ] [
		uifill .25 .05 [
			uihlist 0 [
				uistretchedimage "data/newui/button_idle_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_idle_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_idle_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		uifill .25 .05 [
			uihlist 0 [
				uistretchedimage "data/newui/button_hover_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_hover_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_hover_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		uifill .25 .05 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_selected_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_selected_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_selected_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		if (>= $numargs 3) [
			@@arg3
		]
	]
]

//creates a conditional button with a centred text element
//args: text, cond, action, children
UICondButton = [
	uicondbutton [ @arg2 ] [ @arg3 ] [
		uifill .25 .05 [
			uiclamp 1 1 0 0
			uihlist 0 [
				uistretchedimage "data/newui/button_disabled_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_disabled_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_disabled_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
			UIDisabled
		]
		uifill .25 .05 [
			uihlist 0 [
				uistretchedimage "data/newui/button_idle_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_idle_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_idle_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		uifill .25 .05 [
			uihlist 0 [
				uistretchedimage "data/newui/button_hover_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_hover_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_hover_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		uifill .25 .05 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_selected_left" 0.05 0.05 [ uialign -1 0 ]
				uistretchedimage "data/newui/button_selected_body" 0.15 0.05 [ uiclamp 1 1 0 0 ]
				uistretchedimage "data/newui/button_selected_right" 0.05 0.05  [ uialign 1 0 ]
			]
			uitext [@@@arg1] 1.5 [uialign 0 0]
		]
		if (>= $numargs 4) [
			@@arg4
		]
	]
]

//Creates a tab - only use with aliases
//args: var, trueval, text, contents
UITab = [
	uitoggle [ = $@arg1 @arg2 ] [
		alias @arg1 [@@arg2]
		@arg4
	] 0 [
		uifill 0.16 .04 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_idle_left" 0.04 0.04
				uistretchedimage "data/newui/button_idle_body" 0.08 0.04 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_idle_right" 0.04 0.04
			]
			uitext [@@@arg3] 1.2 [uialign 0 0]
		]
		uifill 0.16 .04 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_idle_left" 0.04 0.04
				uistretchedimage "data/newui/button_idle_body" 0.08 0.04 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_idle_right" 0.04 0.04
			]
			uitext [@@@arg3] 1.2 [uialign 0 0]
			UIHover
		]
		uifill 0.16 .04 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_selected_left" 0.04 0.04
				uistretchedimage "data/newui/button_selected_body" 0.08 0.04 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_selected_right" 0.04 0.04
			]
			uitext [@@@arg3] 1.2 [uialign 0 0]
		]
		uifill 0.16 .04 [
			uihlist 0 [
				uiclamp 1 1 0 0
				uistretchedimage "data/newui/button_selected_left" 0.04 0.04
				uistretchedimage "data/newui/button_selected_body" 0.08 0.04 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_selected_right" 0.04 0.04
			]
			uitext [@@@arg3] 1.2 [uialign 0 0]
			UIHover
		]
	]
]

//Creates a simple checkbox with a text element on the right
//args: Text, Var, trueval, falseval, children
UICheckBox = [
	uitoggle [ || (=f $@arg2 @arg3) (!=f $@arg2 @arg4) ] [
		@arg2 (? (|| (=f $@arg2 @arg3) (!=f $@arg2 @arg4)) @arg4 @arg3)
	] 0 [
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		if (>= $numargs 5) [
			@@arg5
		]
	]
]

//Like the above, but for a bitfield
//args: Text, Val, bits, children
UIBitField = [
	uitoggle [ & $@arg2 @arg3 ] [
		if (& $@arg2 @arg3) [
			@@arg2 (& $@@arg2 (~ @@arg3))
		] [
			@@arg2 (| $@@arg2 @@arg3)
		]
	] 0 [
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		if (>= $numargs 4) [
			@@arg4
		]
	]
]

//Similar to the above two items, but a radio button only has a true value
//args: Text, Var, Trueval, OnSelect, Children
UIRadioButton = [
	uitoggle [ =f $@arg2 @arg3 ] [
		@arg2 @arg3
		if (>= $numargs 4) [
			@@arg4
		]
	] 0 [
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_false_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_idle" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		uihlist 0 [
			uistretchedimage "data/newui/toggle_true_hover" 0.03 0.03
			uioffset 0.015 0
			uitext [@@@arg1] 1.25
		]
		if (>= $numargs 5) [
			@@arg5
		]
	]
]

//Creates a uiscroll area with a uihscrollbar at the bottom
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIHScroller = [
	uivlist 0 [
		uiscroll [@@arg1] (-f [@@arg2] [@@arg3]) [
			@@arg5
		]
		uihscrollbar [@@arg3] [@@arg4] [
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uistretchedimage "data/newui/arrow_idle_left" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uistretchedimage "data/newui/arrow_idle_right" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uistretchedimage "data/newui/arrow_hover_left" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uistretchedimage "data/newui/arrow_idle_right" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uistretchedimage "data/newui/arrow_selected_left" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uistretchedimage "data/newui/arrow_idle_right" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uistretchedimage "data/newui/arrow_idle_left" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uistretchedimage "data/newui/arrow_hover_right" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uistretchedimage "data/newui/arrow_idle_left" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uistretchedimage "data/newui/arrow_selected_right" [@@@@@arg3] [@@@@@arg3]
				]
			]

			uiscrollbutton [
				uistretchedimage "data/newui/scrollbutton_horizontal_idle" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/scrollbutton_horizontal_hover" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/scrollbutton_horizontal_selected" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
			]
		]
	]
]

//Creates a uiscroll area with a uihscrollbar at the right
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIVScroller = [
	uihlist 0 [
		uiscroll (-f [@@arg1] [@@arg3]) [@@arg2] [
			uifill (-f [@@@arg1] [@@@arg3]) [@@@arg2]
			@@arg5
		]
		uivscrollbar [@@arg3] [@@arg4] [
			uifill [@@@arg3] [@@arg2]  [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uistretchedimage "data/newui/arrow_idle_down" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_hover_up" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uistretchedimage "data/newui/arrow_idle_down" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_selected_up" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uistretchedimage "data/newui/arrow_idle_down" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uistretchedimage "data/newui/arrow_hover_down" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" [@@@@@arg3] [@@@@@arg3]
					uistretchedimage "data/newui/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uistretchedimage "data/newui/arrow_selected_down" [@@@@@arg3] [@@@@@arg3]
				]
			]

			uiscrollbutton [
				uistretchedimage "data/newui/scrollbutton_vertical_idle" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/scrollbutton_vertical_hover" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/scrollbutton_vertical_selected" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
			]
		]
	]
]

//Creates a horizontal slider for adjusting a variable
//args: Var, Min, Max, Length, ArrowSize, ScrollSpeed
UIHSlider = [
	uihslider [@arg1] [@arg2] [@arg3] [@arg5] [@arg6] [
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_hover_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_selected_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uistretchedimage "data/newui/arrow_hover_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uistretchedimage "data/newui/arrow_selected_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uisliderbutton [
			uistretchedimage "data/newui/scrollbutton_horizontal_idle" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
			uistretchedimage "data/newui/scrollbutton_horizontal_hover" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
			uistretchedimage "data/newui/scrollbutton_horizontal_selected" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
		]
	]
]

//like the above, but vertical
//args: Var, Min, Max, Length, ArrowSize, ScrollSpeed
UIVSlider = [
	uivslider [@arg1] [@arg2] [@arg3] [@arg5] [@arg6] [
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_hover_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_selected_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uistretchedimage "data/newui/arrow_idle_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uistretchedimage "data/newui/arrow_hover_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uistretchedimage "data/newui/arrow_idle_left" [@@@@arg5] [@@@@arg5]
				uistretchedimage "data/newui/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uistretchedimage "data/newui/arrow_selected_right" [@@@@arg5] [@@@@arg5]
			]
		]
		uisliderbutton [
			uistretchedimage "data/newui/scrollbutton_vertical_idle" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
			uistretchedimage "data/newui/scrollbutton_vertical_hover" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
			uistretchedimage "data/newui/scrollbutton_vertical_selected" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uivartext [@@@@arg1] 1 [uialign 0 0]
			]
		]
	]
]

//Creates a border and sets the arbitarilty provided to be usable as a button
//Large strigns are truncated
//args: X-Size, Y-Size, command, imagecommand, name, children
UIImageButton = [
	uibutton [ @arg3 ] [
		uicolor .2 .4 .6 .4 @arg1 @arg2 [
			uispace .005 0.005 [
				uiclip (-f @@@arg1 .01) (-f @@@arg2 .01) [
					@@@@arg4
				]
				if (>= $numargs 5) [
					uiclip (-f @@@@arg1 .01) (-f @@@@arg2 .01) [
						uialign 0 1
						uitext [@@@@@@arg5] .75
					]
				]
			]
			UIIdle
		]
		uicolor .2 .4 .6 .4 @arg1 @arg2 [
			uispace .005 0.005 [
				uiclip (-f @@@arg1 .01) (-f @@@arg2 .01) [
					@@@@arg4
				]
				if (>= $numargs 5) [
					uiclip (-f @@@@arg1 .01) (-f @@@@arg2 .01) [
						uialign 0 1
						uitext [@@@@@@arg5] .75
					]
				]
			]
			UIHover
		]
		uicolor .2 .4 .6 .4 @arg1 @arg2 [
			uispace .005 0.005 [
				uiclip (-f @@@arg1 .01) (-f @@@arg2 .01) [
					@@@@arg4
				]
				if (>= $numargs 5) [
					uiclip (-f @@@@arg1 .01) (-f @@@@arg2 .01) [
						uialign 0 1
						uitext [@@@@@@arg5] .75
					]
				]
			]
			UISelected
		]
		if (>= $numargs 6) [
			@@arg6
		]
	]
]
