// standardowa biblioteka języka konsoli

// wykonuje dostarczony zbiór
// np. do [@stuff]
do = [ arg1 ]

// łączy wszystkie wyniki każdego przebiegu
// np. loopconcat i 5 [result $i]
// daje wynik "0 1 2 3 4"
loopconcat = [
    sum = "";
    loop $arg1 $arg2 [sum = (concat $sum (arg3))]
    result $sum;
]

// tworzy makro, którego treścią jest sformatowany ciąg
// np. macro greet [ say Hi, %1! ]
macro = [
    $arg1 = (concat [format [@@arg2]] (loopconcat i $numargs [concatword " $arg" (+ $i 1)]))
]

// przypisuje klawisz do przełączania zmiennej
// np. bindvar 9 thirdperson
bindvar = [
  bind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]

// tak samo jak powyżej, ale dotyczy tylko trybu edycji
editbindvar = [
  editbind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]

// przypisuje klawisz tak, że ustawia modyfikator, gdy jest wciśnięty
bindmod = [
  bind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]

// tak samo jak powyżej, ale dotyczy tylko trybu edycji
editbindmod = [
  editbind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]

quine = [ echo (format "quine = [%1]" $quine) ]

loadsky = [
    skybox $arg1
    if (> $numargs 1) [spinsky $arg2]
]

//wyszukaj wyrażenie na liście: arg1 = wyrażenie, arg2 = lista
//zwraca indeks znalezionego wyrażenia lub -1
matchlist = [
	_ret = -1
	looplist i $arg2 [
		if (strcmp $arg1 $i) [ _ret = (listindex $arg2 $i) ]
	] 
	result $_ret
]